
;; Referencias:
;; - http://code.google.com/p/cl-en/source/browse/trunk/basics.lisp#148
;; - http://www.ibm.com/developerworks/xml/tutorials/x-usax/
;; - http://common-lisp.net/project/cxml/
;; - http://common-lisp.net/project/cxml/saxoverview/index.html

(ql:quickload :cxml)
(ql:quickload :cl-ppcre)
(ql:quickload :csv-parser)
(ql:quickload :uri-template)

(uri-template:enable-uri-template-syntax)

(defparameter *fields* '(("BC" . bc) 
			 ("WN-3.0-Synset" . id)
			 ("PT-Words-Man"  . words-man)
			 ("PT-Word-Cand"  . words-sug)
			 ("PT-Gloss"      . gloss-man)
			 ("PT-Gloss-Sug"  . gloss-sug)
			 ("EN-Gloss"      . gloss-en)
			 ("EN-Words"      . words-en)
			 ("SPA-Words-Sug" . words-sp)))

(defparameter *EXTRA* nil)
(defparameter *OUTFILE* #P"wn-data-por.tab")

(defclass synset ()
  ((id :initform nil)
   (bc :initform nil)
   (words-man :initform nil)
   (words-sug :initform nil)
   (gloss-man :initform nil)
   (gloss-sug :initform nil)
   (gloss-en  :initform nil)
   (words-en  :initform nil)
   (words-sp  :initform nil)))


(defclass sax-handler (sax:default-handler)
  ((current-ss     :initform nil :reader current-wn)
   (current-field  :initform nil :reader current-field)
   (synsets        :initform nil :reader synsets) 
   (stack          :initform nil :reader collected-text)))


(defmethod sax:start-element ((h sax-handler) (namespace t) (local-name t) (qname t) (attributes t))
  (with-slots (current-ss current-field stack) h
    (cond 
      ((equal local-name "row") 
       (setf current-ss (make-instance 'synset)))
      ((assoc local-name *fields* :test 'equal) 
       (setf current-field (cdr (assoc local-name *fields* :test 'equal)))
       (setf stack nil)))))


(defmethod sax:end-element ((h sax-handler) (namespace t) (local-name t) (qname t))
  (with-slots (current-ss current-field stack synsets) h
    (cond 
      ((equal local-name "row") 
       (push current-ss synsets))
      ((assoc local-name *fields* :test 'equal) 
       (setf (slot-value current-ss current-field) (format nil "窿蝈鲥蝮篝徙氅┅箦翩篝徙铋飑┅┅ㄤ彐礤翳镤筢恒栳蜥泗弪è筢栳钿戾颟ㄤ狒舂鏖翳箪雉篝徙氅瘐箬溽翎篝徙氅┅ㄤ彐躅簌铙弭骘蝽狒簌铙弭篝蝈犴戾舄è殇箪雉鲠祯簌铙弭ч洎ㄩ洵痫篚怏羼殇暴ㄩ洵镦骟弭ㄦ矧磲铋脯О洧疳蝮瀛轭翦珏篚怏羼殇暴┅翳瀛箪雉黠蜾蟓磲瞟ㄩㄥ聃犰箪雉鲠祯簌铙弭黠蜾蟓磲瞟箦翩翳瀛箪雉黠蜾蟓篚绌ㄤ镬轶ㄣ飙痧泸搴箴扉④荏ì哗苘螵篝蜷铉趄轫Ж＼羽徙＼葬猢箪雉鲠祯簌铙弭翳瀛箪雉┅┅戾è蝈扉篝ㄦ矧磲铋岘幄殇镦骟弭殇痫螬㈧屙磲鳗┅ㄩ咆砸联痱镧钽镱蝈扉篝翳瀛箪雉┅钽镱蝈扉篝Ｕ梏麴函祜玳泱屙狃骁霎怛黝嘲痿鼾┅┅ㄣ篥疳蝮弪瑚蜷翦泱霏扉铄篝蝈犴蝈绌┅┅换躞轭翳疳蝮弪犷泱骘蝽狒翦换戾è睐磲脲轭篝犷沐筢栳钿戾颟┅换ㄣ盱吼狎箦Ｐ阵弪蟑狎徜屙犭弪黠螂罪蜾五舡乱貅瞽痿箫螋邃徼盱睐换磲疸狎灬礅溽螬换扉篝箪雉鲠祯ч洎换箪雉鲠祯黠蜾蟓磲瞟换箪雉鲠祯黠蜾蟓篚绌┅换箪雉鲠祯睐簌铙弭螬┅箦翩泱霏疳蝮弪邯骈屐洵箦疳蜥麸颡＼葬猢鏖翳镳孱骈戾秕险云商弄轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴黩轸瀛扉铄橡孱孜性痫梏麴蠛玳翳踱泔懑狎徜屙犭弪黠蜾铄舡怛妹怡恿钞阿秕舂ㄤ镬轶ㄦ殪ㄤ轵邈麸蝙Ｐ阵弪蟑狎徜屙犭弪黠螂罪蜾五舡乱貅瞽盱┅戾è睐磲脲轭篝犷沐筢栳钿戾颟┅ㄣ盱吼狎箦骈戾睐磲疸狎灬礅溽螬簌铙弭骘蝽狒秕舂箪雉鲠祯睐簌铙弭螬┅┅ㄥ轸